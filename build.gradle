plugins {
    id 'java-library'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'javax.servlet:javax.servlet-api:3.1.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.+'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.+'
}


group = findProperty('group') ?: 'org.mifmi'
if (findProperty('version') == 'unspecified') { version = '' }
version = findProperty('version') ?: System.getenv('RELEASE_VERSION') ?: '0.0.0-SNAPSHOT'
version = (version.startsWith('v')) ? version.substring(1) : version


test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
	
    withJavadocJar()
    withSourcesJar()
}

jar {
    into('META-INF') {
        from 'LICENSE.txt'
        from 'NOTICE.txt'
    }
}

sourcesJar {
    into('META-INF') {
        from 'LICENSE.txt'
        from 'NOTICE.txt'
    }
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    options.locale = 'en_US'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/mozq/enigma4j"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
